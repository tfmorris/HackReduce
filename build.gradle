version = '0.3'

// Modify these values if you want to build against different versions of Hadoop
project.ext.set("hadoop_version", "0.20.2-cdh3u5")


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'org.hackreduce'

sourceCompatibility = 1.6

defaultTasks "clean", "jar"

jar {
  manifest {
      attributes 'Implementation-Title': 'HackReduce', 'Implementation-Version': version
  }
}

repositories {
  maven {
    url "https://repository.cloudera.com/artifactory/cloudera-repos"
  }
  mavenCentral()
}

configurations {
  all*.exclude group: 'com.cloudera.cdh', module: 'hadoop-ant'
}

dependencies {
  compile 'org.apache.hadoop:hadoop-core:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-streaming:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-tools:' + hadoop_version
  compile 'org.apache.hadoop:hadoop-test:' + hadoop_version
}

def pomConfig = {
  name 'HackReduce'
  description 'HackReduce related code'
  url 'http://hackreduce.org'
  organization {
    name 'Hopper'
    url 'http://hopper.com'
  }
  issueManagement {
    system 'Github'
    url 'https://github.com/hackreduce/Hackathon/issues'
  }
  scm {
    url "https://github.com/hackreduce/Hackathon"
    connection "scm:git:http://github.com/hackreduce/Hackathon.git"
    developerConnection "scm:git:git@github.com:hackreduce/Hackathon.git"
  }
  licenses {
    license {
      name 'MIT License'
      url 'http://www.opensource.org/licenses/mit-license.php'
      distribution 'repo'
    }
  }
  developers {
    developer {
      id 'hopper-team'
      name 'Hopper Team'
      organization 'Hopper'
      organizationUrl 'http://hopper.com'
    }
  }
}

configure(install.repositories.mavenInstaller) {
  pom.project pomConfig
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}
